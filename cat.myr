use std

const copy = {fromname, from, to
	var buf : byte[4096]
	while true
		match std.read(from, buf[:])
		| `std.Ok 0: 
			break
		| `std.Ok sz:
			match std.writeall(to, buf[0:sz])
			| (_, `std.Some e):
				std.fatal("writing output failed: {}\n", e)
			| _:
			;;
		| `std.Fail e:
			std.fatal("error reading input {}: {}\n", fromname, e)
		;;
	;;
}

const main = {args : byte[:][:]
	if args.len == 1 
		copy("stdin", 0, 1)
		-> 
	;;
	for file in args[1:]
		match std.open(file, std.Ordonly)
		| `std.Ok fd :
			copy(file, fd, 1)
		| `std.Fail _ : 
			std.fatal("error opening {}\n", file)
		;;
	;;
}
